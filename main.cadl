import "@cadl-lang/rest";
import "@cadl-lang/openapi3";

@serviceTitle("Storage Queues")
@serviceVersion("2021-03-25")
namespace Storage.Queues;

using Cadl.Http;

@doc("Storage Queues Messages")
@route("/{queueName}/messages")
namespace Storage.Queues.Messages {
  @doc("Puts message on the queue.")
  @post
  op putMessage(
    @header contentType: "application/xml",
    @header accept: "application/xml",
    @path queueName: string,
    @query visibilitytimeout?: int32,
    @query messagettl?: int32,
    @body message: QueueMessage
  ): CreatedResponseWithBody<QueueMessagesList>;
}

model QueueMessage {
  MessageText: string;
}

model CreatedResponseWithBody<T> extends CreatedResponse {
  @doc("The reponse body.")
  @body
  body: T;
}

model QueueMessagesList {
  QueueMessage: QueueMessageWithDetails;
}

model QueueMessageWithDetails {
  MessageId: string;
  InsertionTime: zonedDateTime;
  ExpirationTime: zonedDateTime;
  PopReceipt: string;
  TimeNextVisible: zonedDateTime;
}
